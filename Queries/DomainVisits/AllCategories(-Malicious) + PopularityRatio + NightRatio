let DeviceList = dynamic([""]);  // Add DeviceIds here

let DomainList = externaldata(Domain: string, Category: string) ["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/DomainLists/EverythingElse.csv"] with (format="csv", ignoreFirstRecord=True);

let Top10k = externaldata(Domain: string) ["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/DomainLists/top-10k.csv"] with (format="csv", ignoreFirstRecord=True);

// Step 1: Join and flag category + important domains
let CategorizedEvents = DeviceNetworkEvents
//| where DeviceId in (DeviceList)
| join kind=leftouter (DomainList) on $left.RemoteUrl == $right.Domain
| extend 
    Adult = iff(Category == "Porn", 1, 0),
    AI = iff(Category == "AI", 1, 0),
    Crypto = iff(Category == "Crypto", 1, 0),
    Entertainement = iff(Category == "Entertainement", 1, 0),
    Gambling = iff(Category == "Gambling", 1, 0),
    Gaming = iff(Category == "Gaming", 1, 0),
    Piracy = iff(Category == "Piracy", 1, 0),
    Social = iff(Category == "Social", 1, 0),
    IsPopular = iff(RemoteUrl in (Top10k), 1, 0)
| where Category in~ ("Porn", "AI", "Crypto", "Entertainement", "Gambling", "Gaming", "Piracy", "Social");

// Step 2: Compute per-device category flags and diversity
let PerCategoryStats = CategorizedEvents
| summarize 
    Adult = max(Adult), AI = max(AI), Crypto = max(Crypto),
    Entertainement = max(Entertainement), Gambling = max(Gambling), Gaming = max(Gaming),
    Piracy = max(Piracy), Social = max(Social)
  by DeviceId
| extend Diversity = iff(Adult + AI + Crypto + Entertainement + Gambling + Gaming + Piracy + Social > 1, 1, 0);

// Step 3a: Compute Popularity ratio
let Top10kRatioStats = CategorizedEvents
| summarize 
    TotalCategorized = count(), 
    Top10kCount = sum(IsPopular)
  by DeviceId
| extend NonTop10kRatio = strcat(1 - round(todouble(Top10kCount) / todouble(TotalCategorized) * 100, 2), "%");

// Step 3b: Compute NightTime ratio
let NightRatioStats = CategorizedEvents
| extend EventHour = datetime_part("hour", Timestamp)
| extend IsNight = iff(EventHour >= 19 or EventHour < 7, 1, 0)
| summarize 
    TotalCategorized = count(),
    NightCount = sum(IsNight)
  by DeviceId
| extend NightRatio = strcat(round(todouble(NightCount) / todouble(TotalCategorized) * 100, 2), "%");

// Step 4: Join all together
PerCategoryStats
| join kind=inner (Top10kRatioStats) on DeviceId
| join kind=inner (NightRatioStats) on DeviceId
| project-away DeviceId1, DeviceId2, TotalCategorized1

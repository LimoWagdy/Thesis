let DeviceList = dynamic([""]);  // Add DeviceIds here

let DomainList = externaldata(Domain: string, Category: string) 
["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/DomainLists/EverythingElse.csv"] 
with (format="csv", ignoreFirstRecord=True);

let MaliciousList = externaldata(Domain: string, Category: string) 
["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/DomainLists/Malicious.csv"] 
with (format="csv", ignoreFirstRecord=True);

let Top10k = externaldata(Domain: string) 
["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/DomainLists/top-10k.csv"] 
with (format="csv", ignoreFirstRecord=True);

// Step 1: Categorize events
let CategorizedEvents = DeviceNetworkEvents
| join kind=leftouter (DomainList) on $left.RemoteUrl == $right.Domain
| join kind=leftouter (MaliciousList | project Domain, CategoryM = Category) on $left.RemoteUrl == $right.Domain
| extend 
    Adult = iff(Category == "Porn", 1, 0),
    AI = iff(Category == "AI", 1, 0),
    Crypto = iff(Category == "Crypto", 1, 0),
    Entertainement = iff(Category == "Entertainement", 1, 0),
    Gambling = iff(Category == "Gambling", 1, 0),
    Gaming = iff(Category == "Gaming", 1, 0),
    Piracy = iff(Category == "Piracy", 1, 0),
    Social = iff(Category == "Social", 1, 0),
    Malicious = iff(CategoryM == "Malicious", 1, 0),
    IsPopular = iff(RemoteUrl in (Top10k), 1, 0),
    IsUnknown = iff(isempty(Category) and isempty(CategoryM), 1, 0)
| where Category in~ ("Porn", "AI", "Crypto", "Entertainement", "Gambling", "Gaming", "Piracy", "Social") 
   or CategoryM == "Malicious" or IsUnknown == 1;

// Step 2: Per-device category presence + diversity
let PerCategoryStats = CategorizedEvents
| summarize 
    Adult = max(Adult), AI = max(AI), Crypto = max(Crypto),
    Entertainement = max(Entertainement), Gambling = max(Gambling),
    Gaming = max(Gaming), Malicious = max(Malicious),
    Piracy = max(Piracy), Social = max(Social), Unknown = max(IsUnknown)
  by DeviceId
| extend Diversity = iff(Adult + AI + Crypto + Entertainement + Gambling + Gaming + Malicious + Piracy + Social > 1, 1, 0);

// Step 3a: Popularity ratio (excluding Unknown domains from denominator)
let Top10kRatioStats = CategorizedEvents
| where IsUnknown == 0
| summarize 
    TotalCategorized = count(), 
    Top10kCount = sum(IsPopular)
  by DeviceId
| extend NonTop10kRatio = strcat(round((1 - todouble(Top10kCount) / todouble(TotalCategorized)) * 100, 2), "%");

// Step 3b: Night ratio (excluding Unknown domains from denominator)
let NightRatioStats = CategorizedEvents
| where IsUnknown == 0
| extend EventHour = datetime_part("hour", Timestamp)
| extend IsNight = iff(EventHour >= 19 or EventHour < 7, 1, 0)
| summarize 
    TotalCategorized = count(),
    NightCount = sum(IsNight)
  by DeviceId
| extend NightRatio = strcat(round(todouble(NightCount) / todouble(TotalCategorized) * 100, 2), "%");

// Step 4: Join all
PerCategoryStats
| join kind=leftouter (Top10kRatioStats) on DeviceId
| join kind=leftouter (NightRatioStats) on DeviceId
| project-away DeviceId1, DeviceId2, TotalCategorized1

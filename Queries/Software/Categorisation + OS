let DeviceList = externaldata(Customer: string, Workspace: string, DeviceId: string)
["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/Queries/QueryCSVs/AlertIds.csv"]
with (format="csv", ignoreFirstRecord=true);

let LocalCertList = externaldata(SubjectCN: string, Category: string)
["https://raw.githubusercontent.com/LimoWagdy/Thesis/refs/heads/main/SoftwareCerts/AllCombined.csv"]
with (format="csv", ignoreFirstRecord=true);

let LatestOSVersions = datatable(OSPlatform:string, LatestBuild:string) [
    "Windows11", "10.0.26100.4770",
    "Windows10", "10.0.19045.6159",
    "macOS", "15.6.0.0"
];

// Certificate-based category flags
let CertCategoryStats = DeviceFileCertificateInfo
| join kind=inner (DeviceList | project DeviceId) on DeviceId
| join kind=leftouter (LocalCertList) on $left.Signer == $right.SubjectCN
| extend 
    Crypto = iff(isnotempty(SubjectCN) and Category == "Crypto", 1, 0),
    Gambling = iff(isnotempty(SubjectCN) and Category == "Gambling", 1, 0),
    Gaming = iff(isnotempty(SubjectCN) and Category == "Gaming", 1, 0),
    Suspicious = iff(isnotempty(SubjectCN) and Category == "Suspicious", 1, 0),
    Media = iff(isnotempty(SubjectCN) and Category == "Media", 1, 0),
    Piracy = iff(isnotempty(SubjectCN) and Category == "Piracy", 1, 0),
    Unknown = iff(isempty(SubjectCN) and isempty(Category), 1, 0)
| summarize 
    Crypto = max(Crypto), Gambling = max(Gambling), Gaming = max(Gaming), 
    Suspicious = max(Suspicious), Media = max(Media), Piracy = max(Piracy), Unknown = max(Unknown)
  by DeviceId
| extend Diversity = Crypto + Gambling + Gaming + Suspicious + Media + Piracy;

// OS version check
let OSVersionStatus = DeviceTvmSoftwareInventory
| join kind=inner (DeviceList | project DeviceId) on DeviceId
| join kind=inner LatestOSVersions on OSPlatform
| project DeviceId, OSPlatform, OSVersion, 
          IsUpToDate = iff(OSVersion == LatestBuild, 1, 0);

// Final join
CertCategoryStats
| join kind=inner (OSVersionStatus) on DeviceId
| project-away DeviceId1
| distinct *

//leftanti in DeviceList table joins to retrieve controls
